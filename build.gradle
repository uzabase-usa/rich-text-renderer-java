plugins {
    id("base")
    id("java")
    id("maven-publish")
}

allprojects {
    group = "com.github.contentful.rich"

    version = '1.4.0'
    ext.contentful_version = "10.5.6"

    repositories {
        mavenCentral()
        google()
        maven { url 'https://www.jitpack.io' }
    }
}

dependencies {
    // Make the root project archives configuration depend on every sub project
    subprojects.forEach {
        archives(it)
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "java"
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/uzabase-usa/rich-text-renderer-java")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}